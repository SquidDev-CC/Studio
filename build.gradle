apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.8
version = '0.1'

repositories {
	mavenCentral()

	maven {
		name = "squiddev"
		url = "http://maven.bonzodandd.co.uk"
	}

	ivy {
		name "ComputerCraft"
		artifactPattern "http://addons-origin.cursecdn.com/files/2228/723/[module][revision].[ext]"
	}
}

dependencies {
	// Core Libraries
	compile group: 'org.ow2.asm', name: 'asm-all', version: '5.+'
	compile group: 'ComputerCraft', name: 'ComputerCraft', version: '1.73', ext: 'jar'

	// TerminalOutput dependencies
	compile group: 'jline', name: 'jline', version: '2.+'

	// LWJGL dependencies
	// http://build.lwjgl.org/stable/lwjgl.zip
	// compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.9.3'
	compile files('lib/jar/lwjgl.jar')

	testCompile group: 'junit', name: 'junit', version: '4.+'

	// Java busted sources
	testCompile (group: 'squiddev', name: 'squiddev.busted', version: '1.+') {
		exclude group: 'org.luaj'
	}
}

// Download LWJGL from online
task getLwjgl(type: Copy) {
	def f = file('lib/jar/lwjgl.jar')
	if (!f.exists()) {
		file("lib").mkdirs();
		def output = file("lib/lwjgl.zip")

		new URL('http://build.lwjgl.org/stable/lwjgl.zip').withInputStream {
			i -> output.withOutputStream { it << i }
		}
		from zipTree(output)
		into "lib"
	}
}

compileJava.dependsOn getLwjgl

uploadArchives {
	repositories {
		flatDir {
			dirs 'build'
		}
	}
}

sourceSets {
	main {
		resources {
			exclude '**/__ignore/*'
		}
	}
}

test {
	filter {
		includeTestsMatching "squidev.*"
	}
}
