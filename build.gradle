group 'org.squiddev'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.6

repositories {
	mavenCentral()

	ivy { artifactPattern "http://addons-origin.cursecdn.com/files/2291/384/[module][revision](.[ext])" }

	maven { url = "https://dl.bintray.com/squiddev/maven" }
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	compile "ComputerCraft:ComputerCraft:1.79"
	compile "com.googlecode.lanterna:lanterna:3.0.0-beta2"
	// compile 'com.google.guava:guava:19.0'
	compile "com.google.code.gson:gson:2.6.2"
	compile "org.ow2.asm:asm-debug-all:5.0.4"

	shade('org.squiddev:cctweaks-lua:1.0.2') {
		// Different versions
		exclude group: 'ComputerCraft'
		// exclude group: 'com.google.guava'
		exclude group: 'net.minecraftforge'
		exclude module: 'ConfigGen'
	}

	testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
	from configurations.shade
		.findAll { it.getAbsolutePath().contains("org.squiddev") }
		.collect { it.isDirectory() ? it : zipTree(it) }

	// Package the patch files into a patch/ folder
	filesMatching("org/luaj/**", {
		it.path = "patch/" + it.path
	})
}

task runTerminal(type: JavaExec) {
	main "org.squiddev.studio.Launch"
	classpath += sourceSets.main.runtimeClasspath
}

runTerminal.dependsOn jar
