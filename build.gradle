apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.7

group = 'org.squiddev'
version = '0.1'

repositories {
	mavenCentral()

	maven {
		name = "squiddev"
		url = "http://maven.bonzodandd.co.uk"
	}

	ivy {
		name "ComputerCraft"
		artifactPattern "http://addons-origin.cursecdn.com/files/2228/723/[module][revision].[ext]"
	}
}

dependencies {
	// Core Libraries
	compile group: 'ComputerCraft', name: 'ComputerCraft', version: '1.73', ext: 'jar'
	compile group: 'org.squiddev', name: 'luaj.api', version: '1.+'
	compile group: 'org.squiddev', name: 'luaj.luajc', version: '1.+'

	// TerminalOutput dependencies
	compile group: 'jline', name: 'jline', version: '2.+'

	// LWJGL dependencies
	// http://build.lwjgl.org/stable/lwjgl.zip
	compile files('lib/jar/lwjgl.jar')

	testCompile group: 'junit', name: 'junit', version: '4.+'
}

// We provide a custom LuaJ source with ComputerCraft
configurations.all { exclude group: 'org.luaj' }

// Package all the org.squiddev dependencies into one file
jar {
	from configurations.compile
			.findAll { it.getAbsolutePath().contains("org.squiddev") }
			.collect { it.isDirectory() ? it : zipTree(it) }
}

// Download LWJGL from online
task getLwjgl(type: Copy) {
	def f = file('lib/jar/lwjgl.jar')
	// If we don't have the lwjgl file then:
	if (!f.exists()) {
		// Create the directory
		file("lib").mkdirs();
		def output = file("lib/lwjgl.zip")

		// Download it
		new URL('http://build.lwjgl.org/stable/lwjgl.zip').withInputStream {
			i -> output.withOutputStream { it << i }
		}

		// Extract the jar and native folders. We need native to run the code with
		from zipTree(output)
		into "lib"
		include("jar/**", "native/**")
	}
}

compileJava.dependsOn getLwjgl

task runGui(type: JavaExec) {
	main "org.squiddev.ccstudio.output.lwjgl.GuiOutputMain"
	classpath += sourceSets.main.runtimeClasspath
	jvmArgs = ["-Dsquiddev.natives=lib/native"]
}
runGui.dependsOn classes

task runTerminal(type: JavaExec) {
	main "org.squiddev.ccstudio.output.terminal.TerminalOutputMain"
	classpath += sourceSets.main.runtimeClasspath
}
runTerminal.dependsOn classes
